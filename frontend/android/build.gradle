apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'

archivesBaseName = 'sdk-search'

android {
  compileSdkVersion buildConfig.compileSdk

  playAccountConfigs {
    production {
      serviceAccountEmail = propOrEmpty('SDK_SEARCH_UPLOAD_PLAY_EMAIL')
      pk12File = file('upload.p12')
    }
  }

  defaultConfig {
    applicationId 'com.jakewharton.sdksearch'

    minSdkVersion buildConfig.minSdk
    targetSdkVersion buildConfig.targetSdk

    versionCode buildConfig.version.code
    versionName buildConfig.version.name

    resConfigs 'en'

    buildConfigField 'String', 'BUGSNAG_API_KEY', "\"${propOrEmpty('SDK_SEARCH_BUGSNAG_API_KEY')}\""
    buildConfigField 'boolean', 'IS_CI_BUILD', 'false' // May be overridden by a build type.
    buildConfigField 'String', 'COMMIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'COMMIT_UNIX_TIMESTAMP', gitTimestamp().toString() + "L"

    playAccountConfig = playAccountConfigs.production
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    textReport true
    textOutput 'stdout'
    lintConfig rootProject.file('lint.xml')

    // We run a full lint analysis as build part in CI, so skip vital checks for assemble task.
    checkReleaseBuilds false
  }

  signingConfigs {
    debug {
      storeFile file('debug.keystore')
      storePassword 'sdksearch'
      keyAlias 'sdksearch'
      keyPassword 'sdksearch'
    }
    if (file('upload.keystore').exists()) {
      upload {
        storeFile file('upload.keystore')
        storePassword propOrEmpty('SDK_SEARCH_UPLOAD_STORE_PASSWORD')
        keyAlias 'sdksearch'
        keyPassword propOrEmpty('SDK_SEARCH_UPLOAD_KEY_PASSWORD')
      }
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix '.debug'
      signingConfig signingConfigs.debug

      buildConfigField 'boolean', 'IS_CI_BUILD', isCiBuild.toString()
    }
    release {
      if (file('upload.keystore').exists()) {
        signingConfig signingConfigs.upload
      } else {
        signingConfig signingConfigs.debug
      }

      minifyEnabled true
      shrinkResources true
      proguardFiles(
          file('src/main/proguard-android.pro'),
          file('src/main/proguard-app.pro')
      )
    }
  }

  packagingOptions {
    exclude 'kotlin/**'
    exclude 'META-INF/*.kotlin_module'
    exclude 'META-INF/*.version'
  }
}

dependencies {
  implementation project(':references:jdk')
  implementation project(':sync')

  debugImplementation project(':debug-updater')

  implementation deps.kotlin.stdlib.jdk
  implementation deps.kotlin.coroutines.jdk
  implementation deps.kotlin.coroutines.android
  implementation deps.kotlin.coroutines.rx2

  implementation deps.android.support.annotations
  implementation deps.android.support.recyclerView
  implementation deps.android.support.customTabs
  implementation deps.android.support.design
  implementation deps.android.support.constraintLayout
  implementation deps.android.x.coreKtx

  implementation deps.rx.java
  implementation deps.rx.android
  implementation deps.rx.binding.framework
  implementation deps.rx.relay

  implementation deps.timber
  implementation deps.bugsnag
  implementation project(':bugsnag-tree')
}

kotlin {
  experimental {
    coroutines 'enable'
  }
}

play {
  track = 'alpha'
  uploadImages = true
}
