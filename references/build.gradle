apply plugin: 'org.jetbrains.kotlin.multiplatform'

archivesBaseName = 'references'

kotlin {
  jvm()
  js {
    // TODO apply this automatically on all multiplatform projects using js presets.
    compilations.all {
      kotlinOptions {
        sourceMap = true
        moduleKind = 'umd'
        metaInfo = true
      }
    }
  }

  sourceSets {
    commonMain {
      dependencies {
        api deps.kotlin.stdlib.common
      }
    }
    commonTest {
      dependencies {
        implementation deps.kotlin.test.common
        implementation deps.kotlin.test.annotations
      }
    }
    jvmMain {
      dependencies {
        api deps.kotlin.stdlib.jdk
      }
    }
    jvmTest {
      dependencies {
        implementation deps.kotlin.test.jdk
      }
    }
    jsMain {
      dependencies {
        api deps.kotlin.stdlib.js
      }
    }
    jsTest {
      dependencies {
        implementation deps.kotlin.test.js
      }
    }
  }
}

// TODO run this automatically on all projects applying multiplatform plugin.
def jsTargets = kotlin.targets.matching { it.platformType.name == 'js' }
if (!jsTargets.isEmpty()) {
  apply plugin: 'com.moowork.node'

  node {
    nodeModulesDir = buildDir
  }

  def npmInstallMocha = tasks.create('npmInstallMocha', NpmTask) {
    args = ['install', 'mocha@4.1.0', 'source-map-support@0.5.3', '--no-save']
  }

  jsTargets.all { target->
    def mainCompilation = target.compilations['main']
    def mainCompilationTask = tasks[mainCompilation.compileKotlinTaskName]

    def testCompilation = target.compilations['test']
    def testCompilationTask = tasks[testCompilation.compileKotlinTaskName]

    // Combine all required compilation results in the node_modules folder
    // - main compilation result (compileKotlinJs)
    // - all runtime dependencies of test sourceset
    def dependenciesTask = task("aggregate${target.name}TestDependencies", type: Sync) {
      from(mainCompilationTask.destinationDir)
      into("$node.nodeModulesDir/node_modules")

      def configuration = testCompilation.runtimeDependencyFiles
      from(files {
        configuration.collect { File file ->
          file.name.endsWith('.jar') ?
              zipTree(file.absolutePath).matching {
                include '*.js'
                include '*.js.map'
              } :
              files()
        }
      }.builtBy(configuration))
    }
    dependenciesTask.dependsOn(mainCompilationTask)
    npmInstallMocha.dependsOn(dependenciesTask)

    def mochaTask = task("${target.name}TestWithMocha", type: NodeTask) {
      group = 'verification'
      description = "Runs tests with Mocha for 'test' compilation of target '${target.name}'"

      script = file("$node.nodeModulesDir/node_modules/mocha/bin/mocha")
      args = [testCompilationTask.outputFile, '--require', 'source-map-support/register']
    }
    mochaTask.dependsOn(testCompilationTask, npmInstallMocha)

    tasks.getByName("${target.name}Test").dependsOn(mochaTask)
  }
}
